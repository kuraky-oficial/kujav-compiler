// src/parser/kujav.pest
program = { SOI ~ (declaration | statement)* ~ EOI }
declaration = { fun_decl | let_decl }
let_decl    = { "let" ~ identifier ~ "=" ~ expression }

fun_decl    = { "fun" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ (":" ~ identifier)? ~ "{" ~ block ~ "}" }
parameter_list = { identifier ~ ("," ~ identifier)* }
argument_list  = { expression ~ ("," ~ expression)* }

statement = { while_stmt | if_stmt | print_stmt | index_assign_stmt | call_stmt | let_decl | return_stmt }

index_assign_stmt = { identifier ~ "[" ~ expression ~ "]" ~ "=" ~ expression }
print_stmt = { "print" ~ expression }
return_stmt = { "return" ~ expression }
call_stmt  = { call_expr }
call_expr  = { identifier ~ "(" ~ argument_list? ~ ")" }

if_stmt    = { "if" ~ expression ~ "{" ~ block ~ "}" ~ ("else" ~ "{" ~ block ~ "}")? }
while_stmt = { "while" ~ expression ~ "{" ~ block ~ "}" }
block      = { (declaration | statement)* }

expression = { comp_expr ~ (logic_op ~ comp_expr)* }
logic_op   = { "and" | "or" }
comp_expr  = { term ~ (comp_op ~ term)* }
comp_op    = { "==" | "<" | ">" }
term       = { factor ~ (add_op ~ factor)* }
add_op     = { "+" | "-" }
factor     = { primary ~ (mul_op ~ primary)* }
mul_op     = { "*" | "/" }

primary = { string | number | boolean | input_expr | array_lit | array_access | "(" ~ expression ~ ")" | call_expr | identifier }
boolean    = { "true" | "false" }
input_expr = { "input" ~ "(" ~ ")" }
// Nueva regla para literales: [1, 2, 3]
array_lit = { "[" ~ (expression ~ ("," ~ expression)*)? ~ "]" }

// Nueva regla para acceso: lista[5]
array_access = { identifier ~ "[" ~ expression ~ "]" }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number     = @{ ASCII_DIGIT+ }
string     = @{ "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "--" ~ (!"\n" ~ ANY)* }