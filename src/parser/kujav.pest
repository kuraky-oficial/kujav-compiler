// src/parser/kujav.pest
program = { SOI ~ (declaration | statement)* ~ EOI }

declaration = { fun_decl | let_decl }
let_decl    = { "let" ~ identifier ~ "=" ~ expression }
fun_decl    = { "fun" ~ identifier ~ "(" ~ parameter_list? ~ ")" ~ "{" ~ block ~ "}" }

parameter_list = { identifier ~ ("," ~ identifier)* }

// Estilo Lua: print sin par√©ntesis
statement = { while_stmt | if_stmt | print_stmt | let_decl }
print_stmt = { "print" ~ expression }

// Estructuras de control
if_stmt    = { "if" ~ expression ~ "{" ~ block ~ "}" ~ ("else" ~ "{" ~ block ~ "}")? }
while_stmt = { "while" ~ expression ~ "{" ~ block ~ "}" }
block      = { (declaration | statement)* }

// Expresiones y Comparadores
expression = { term ~ (comp_op ~ term)* }
comp_op    = { "==" | "<" | ">" }

term   = { factor ~ (add_op ~ factor)* }
add_op = { "+" | "-" }

factor = { primary ~ (mul_op ~ primary)* }
mul_op = { "*" | "/" }

primary = { string | number | identifier }

identifier = @{ ASCII_ALPHA ~ (ASCII_ALPHANUMERIC | "_")* }
number     = @{ ASCII_DIGIT+ }
string     = { "\"" ~ (!"\"" ~ ANY)* ~ "\"" }

WHITESPACE = _{ " " | "\t" | "\r" | "\n" }
COMMENT    = _{ "--" ~ (!"\n" ~ ANY)* } // Comentarios estilo Lua --